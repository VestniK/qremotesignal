import os

Import('BaseEnv')

env = BaseEnv.Clone()
env.EnableQt4Modules(['QtCore'])

env.Append(CPPPATH = ['.'])
env.Append(CPPDEFINES=['BUILD_QRS_SHARED_LIB'])

if  not (ARGUMENTS.get('nocheck') or GetOption('clean') or GetOption('help') ) :
   conf = Configure(env)
   if not conf.CheckLibWithHeader('qjson','qjson/parser.h','c++'): Exit(1)
   conf.Finish()

env['GchSh'] = env.GchSh('qremotesignal_pch.h')
env.NoClean(env['GchSh'])
env.Append(CPPFLAGS=Split('-include qremotesignal_pch.h'))

Sources = ['baseconverters.cpp','servicesmanager.cpp',
           'jsonserializer.cpp','message.cpp',
           'qdatastreamserializer.cpp']

InternalsSources = ['devicemanager.cpp']

Headers = ['QRemoteSignal','absservice.h','baseconverters.h',
           'servicesmanager.h','baseexception.h',
           'message.h','absmessageserializer.h',
           'jsonserializer.h','serializationexceptions.h',
           'qrsexport.h','globalserializer.h',
           'qdatastreamserializer.h']

InternalsLib = env.StaticLibrary('QrsInternals',InternalsSources);
lib = env.SharedLibrary('QRemoteSignal',Sources, LIBS=['$LIBS',InternalsLib])

Pc = env.Config('QRemoteSignal.pc.in')
CMakeConfig = env.Config('QRemoteSignalConfig.cmake.in')
qmake_feature = env.Config('qremotesignal.prf.in')
env.AlwaysBuild( qmake_feature )
env.AlwaysBuild(CMakeConfig)
env.AlwaysBuild(Pc)

env.InstallLibrary(lib)
env.InstallHeader(Headers)
env.InstallPkgConfig(Pc)
env.InstallQMakeFeature(qmake_feature)
env.InstallCMakeConfig(CMakeConfig)

BaseEnv['QRemoteSignalLib'] = lib
BaseEnv['QrsInternalsLib'] = InternalsLib
