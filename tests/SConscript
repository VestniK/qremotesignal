Import('BaseEnv')

env = BaseEnv.Clone()
env.Tool('qrs_dev_build')
env['CPPPATH']=['.','#lib']
env.EnableQt4Modules(['QtCore','QtTest'])
env['LIBS'] = BaseEnv['QRemoteSignalStatic'] + env['LIBS']
env['LIBS']+=['qjson']

Service = env.QRSService('example.xml')
Client  = env.QRSClient('example.xml')

remotesignaltests = env.Program('remotesignaltests',Service+Client+['remotesignaltests.cpp'])
errortests = env.Program('errortests',Service+['errortests.cpp'])
jsonserializertests = env.Program('jsonserializertests',['jsonserializertests.cpp'])
converterstests = env.Program('converterstests',['converterstests.cpp'])

customtypestests = SConscript('customtypestest/SConscript',exports='env')
json = SConscript('serializers/SConscript',exports='env')

env['BUILDERS']['QTest'] = Builder(action=('$SOURCE -xml -o $TARGET'),
                                   src_suffix='',
                                   suffix='.xml')

env.QTest('reports/remotesignaltests.xml',remotesignaltests)
env.QTest('reports/errortests.xml',errortests)
env.QTest('reports/jsonserializertests.xml',jsonserializertests)
env.QTest('reports/customtypestests.xml',customtypestests)
env.QTest('reports/converterstests.xml',converterstests)
env.QTest('reports/json.xml',json)
